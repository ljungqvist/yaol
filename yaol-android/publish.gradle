apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/yaol-android-release.aar")
            artifact sourcesJar
            groupId yaol_group
            artifactId 'yaol-android'
            version yaol_version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    configurations = ['archives']
    publish = true
    override = true
    pkg {
        repo = 'yaol'
        name = 'yaol-android'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:ljungqvist/yaol.git'
        version {
            name = yaol_version
            released  = new Date()
            vcsTag = "v$yaol_version"
        }
    }
}