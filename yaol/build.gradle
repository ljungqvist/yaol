apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'

    testImplementation "org.junit.platform:junit-platform-runner:1.3.1"
//    testImplementation "org.junit.jupiter:junit-jupiter-api:1.3.1"

    testImplementation ('org.spekframework.spek2:spek-dsl-jvm:2.0.0-alpha.1')  {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly ('org.spekframework.spek2:spek-runner-junit5:2.0.0-alpha.1') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    // spek requires kotlin-reflect, can be omitted if already in the classpath
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId yaol_group
            artifactId 'yaol'
            version yaol_version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    pkg {
        repo = 'yaol'
        name = 'yaol'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:ljungqvist/yaol.git'
        version {
            name = yaol_version
            released  = new Date()
            vcsTag = "v$yaol_version"
        }
    }
}

group yaol_group
version yaol_version

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().getUrl())
        }
    }
}
